apply plugin: 'com.android.application'

android {
    signingConfigs {
        config {
            keyAlias 'debugkey'
            keyPassword 'debugkey'
            storeFile file('../../DebugKeystore.jks')
            storePassword 'debugkey'
        }
    }
    compileSdkVersion 22
    buildToolsVersion '21.1.2'
    defaultConfig {
        applicationId "de.tudarmstadt.informatik.bp.bonfirechat"
        minSdkVersion 19
        //noinspection OldTargetApi
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.config
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'IconDensities'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/asmack-android-8-4.0.7.jar')
    compile files('libs/org.xbill.dns_2.1.7.jar')
    compile files('libs/kalium-jni-1.0.0-SNAPSHOT.jar')
    //noinspection GradleDependency
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.google.android.gms:play-services-gcm:7.8.0'
    compile 'com.google.android.gms:play-services-maps:7.8.0'
    compile 'com.google.zxing:core:3.0.0'
    //noinspection GradleDependency
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.github.amlcurran.showcaseview:library:5.0.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

apply plugin: 'findbugs'

findbugs {
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

apply plugin: 'checkstyle'

task checkstyle(type: Checkstyle) {
    source 'src'
    include '**/*.java'
    exclude '**/gen/**','**/*Test.java'
    classpath = files()
}

// change this to whatever your app's name is...
project.archivesBaseName = 'bonfire'

android.applicationVariants.all { variant ->
    def apkPath = variant.outputs[0].outputFile.parentFile
    def baseName = project.archivesBaseName

    // if it's a release build, the type is implicit, imo, so add it otherwise
    if (variant.name != 'release') {
        baseName += "-${variant.buildType.name}"
    }

    // add version name and version code
    baseName += "-v${variant.mergedFlavor.versionName}-${variant.mergedFlavor.versionCode}"

    // if built on jenkins ci, add jenkins build number:
    def buildNumber = System.getenv('BUILD_NUMBER')
    if (buildNumber && buildNumber.size() > 0) {
        baseName += "-b${buildNumber}"
    }

    // if the variant will not be zipAligned, specify that
    if (!variant.outputs[0].zipAlign) {
        baseName += '-unaligned'
    }

    // set the output file
    variant.outputs[0].outputFile = new File(apkPath, "${baseName}.apk");
}

